/**
 * @file      rasterize.cu
 * @brief     CUDA-accelerated rasterization pipeline.
 * @authors   Skeleton code: Yining Karl Li, Kai Ninomiya, Shuai Shao (Shrek)
 * @date      2012-2016
 * @copyright University of Pennsylvania & STUDENT
 */

#include <cmath>
#include <cstdio>
#include <cuda.h>
#include <cuda_runtime.h>
#include <thrust/random.h>
#include <util/checkCUDAError.h>
#include <util/tiny_gltf_loader.h>
#include "rasterizeTools.h"
#include "rasterize.h"
#include <glm/gtc/quaternion.hpp>
#include <glm/gtc/matrix_transform.hpp>

namespace {

	typedef unsigned short VertexIndex;
	typedef glm::vec3 VertexAttributePosition;
	typedef glm::vec3 VertexAttributeNormal;
	typedef glm::vec2 VertexAttributeTexcoord;
	typedef unsigned char TextureData;

	typedef unsigned char BufferByte;

	enum PrimitiveType{
		Point = 1,
		Line = 2,
		Triangle = 3
	};

	struct VertexOut {
		glm::vec4 pos;

		// TODO: add new attributes to your VertexOut
		// The attributes listed below might be useful, 
		// but always feel free to modify on your own

		 glm::vec3 eyePos;	// eye space position used for shading
		 glm::vec3 eyeNor;	// eye space normal used for shading, cuz normal will go wrong after perspective transformation
		// glm::vec3 col;
		 glm::vec2 texcoord0;
		 TextureData* dev_diffuseTex = NULL;
		// int texWidth, texHeight;
		// ...
		 int diffuseTexWidth, diffuseTexHeight;
	};

	struct Primitive {
		PrimitiveType primitiveType = Triangle;	// C++ 11 init
		VertexOut v[3];
	};

	struct Fragment {
		glm::vec3 color;

		// TODO: add new attributes to your Fragment
		// The attributes listed below might be useful, 
		// but always feel free to modify on your own

		 glm::vec3 eyePos;	// eye space position used for shading
		 glm::vec3 eyeNor;
		// VertexAttributeTexcoord texcoord0;
		// TextureData* dev_diffuseTex;
		// ...
	};

	struct PrimitiveDevBufPointers {
		int primitiveMode;	//from tinygltfloader macro
		PrimitiveType primitiveType;
		int numPrimitives;
		int numIndices;
		int numVertices;

		// Vertex In, const after loaded
		VertexIndex* dev_indices;
		VertexAttributePosition* dev_position;
		VertexAttributeNormal* dev_normal;
		VertexAttributeTexcoord* dev_texcoord0;

		// Materials, add more attributes when needed
		TextureData* dev_diffuseTex;
		int diffuseTexWidth;
		int diffuseTexHeight;
		// TextureData* dev_specularTex;
		// TextureData* dev_normalTex;
		// ...

		// Vertex Out, vertex used for rasterization, this is changing every frame
		VertexOut* dev_verticesOut;

		// TODO: add more attributes when needed
	};

}

static std::map<std::string, std::vector<PrimitiveDevBufPointers>> mesh2PrimitivesMap;


static int width = 0;
static int height = 0;

static int totalNumPrimitives = 0;
static Primitive *dev_primitives = NULL;
static Fragment *dev_fragmentBuffer = NULL;
static glm::vec3 *dev_framebuffer = NULL;

static int * dev_depth = NULL;	// you might need this buffer when doing depth test

/**
 * Kernel that writes the image to the OpenGL PBO directly.
 */
__global__ 
void sendImageToPBO(uchar4 *pbo, int w, int h, glm::vec3 *image) {
    int x = (blockIdx.x * blockDim.x) + threadIdx.x;
    int y = (blockIdx.y * blockDim.y) + threadIdx.y;
    int index = x + (y * w);

    if (x < w && y < h) {
        glm::vec3 color;
        color.x = glm::clamp(image[index].x, 0.0f, 1.0f) * 255.0;
        color.y = glm::clamp(image[index].y, 0.0f, 1.0f) * 255.0;
        color.z = glm::clamp(image[index].z, 0.0f, 1.0f) * 255.0;
        // Each thread writes one pixel location in the texture (textel)
        pbo[index].w = 0;
        pbo[index].x = color.x;
        pbo[index].y = color.y;
        pbo[index].z = color.z;
    }
}

/** 
* Writes fragment colors to the framebuffer
*/
__global__
void render(int w, int h, Fragment *fragmentBuffer, glm::vec3 *framebuffer) {
    int x = (blockIdx.x * blockDim.x) + threadIdx.x;
    int y = (blockIdx.y * blockDim.y) + threadIdx.y;
    int index = x + (y * w);

    if (x < w && y < h) {
        framebuffer[index] = fragmentBuffer[index].color;

		// TODO: add your fragment shader code here

    }
}

/**
 * Called once at the beginning of the program to allocate memory.
 */
void rasterizeInit(int w, int h) {
    width = w;
    height = h;
	cudaFree(dev_fragmentBuffer);
	cudaMalloc(&dev_fragmentBuffer, width * height * sizeof(Fragment));
	cudaMemset(dev_fragmentBuffer, 0, width * height * sizeof(Fragment));
    cudaFree(dev_framebuffer);
    cudaMalloc(&dev_framebuffer,   width * height * sizeof(glm::vec3));
    cudaMemset(dev_framebuffer, 0, width * height * sizeof(glm::vec3));
    
	cudaFree(dev_depth);
	cudaMalloc(&dev_depth, width * height * sizeof(int));

	checkCUDAError("rasterizeInit");
}

__global__
void initDepth(int w, int h, int * depth)
{
	int x = (blockIdx.x * blockDim.x) + threadIdx.x;
	int y = (blockIdx.y * blockDim.y) + threadIdx.y;

	if (x < w && y < h)
	{
		int index = x + (y * w);
		depth[index] = INT_MAX;
	}
}


/**
* kern function with support for stride to sometimes replace cudaMemcpy
* One thread is responsible for copying one component
*/
__global__ 
void _deviceBufferCopy(int N, BufferByte* dev_dst, const BufferByte* dev_src, int n, int byteStride, int byteOffset, int componentTypeByteSize) {
	
	// Attribute (vec3 position)
	// component (3 * float)
	// byte (4 * byte)

	// id of component
	int i = (blockIdx.x * blockDim.x) + threadIdx.x;

	if (i < N) {
		int count = i / n;
		int offset = i - count * n;	// which component of the attribute

		for (int j = 0; j < componentTypeByteSize; j++) {
			
			dev_dst[count * componentTypeByteSize * n 
				+ offset * componentTypeByteSize 
				+ j]

				= 

			dev_src[byteOffset 
				+ count * (byteStride == 0 ? componentTypeByteSize * n : byteStride) 
				+ offset * componentTypeByteSize 
				+ j];
		}
	}
	

}

__global__
void _nodeMatrixTransform(
	int numVertices,
	VertexAttributePosition* position,
	VertexAttributeNormal* normal,
	glm::mat4 MV, glm::mat3 MV_normal) {

	// vertex id
	int vid = (blockIdx.x * blockDim.x) + threadIdx.x;
	if (vid < numVertices) {
		position[vid] = glm::vec3(MV * glm::vec4(position[vid], 1.0f));
		normal[vid] = glm::normalize(MV_normal * normal[vid]);
	}
}

glm::mat4 getMatrixFromNodeMatrixVector(const tinygltf::Node & n) {
	
	glm::mat4 curMatrix(1.0);

	const std::vector<double> &m = n.matrix;
	if (m.size() > 0) {
		// matrix, copy it

		for (int i = 0; i < 4; i++) {
			for (int j = 0; j < 4; j++) {
				curMatrix[i][j] = (float)m.at(4 * i + j);
			}
		}
	} else {
		// no matrix, use rotation, scale, translation

		if (n.translation.size() > 0) {
			curMatrix[3][0] = n.translation[0];
			curMatrix[3][1] = n.translation[1];
			curMatrix[3][2] = n.translation[2];
		}

		if (n.rotation.size() > 0) {
			glm::mat4 R;
			glm::quat q;
			q[0] = n.rotation[0];
			q[1] = n.rotation[1];
			q[2] = n.rotation[2];

			R = glm::mat4_cast(q);
			curMatrix = curMatrix * R;
		}

		if (n.scale.size() > 0) {
			curMatrix = curMatrix * glm::scale(glm::vec3(n.scale[0], n.scale[1], n.scale[2]));
		}
	}

	return curMatrix;
}

void traverseNode (
	std::map<std::string, glm::mat4> & n2m,
	const tinygltf::Scene & scene,
	const std::string & nodeString,
	const glm::mat4 & parentMatrix
	) 
{
	const tinygltf::Node & n = scene.nodes.at(nodeString);
	glm::mat4 M = parentMatrix * getMatrixFromNodeMatrixVector(n);
	n2m.insert(std::pair<std::string, glm::mat4>(nodeString, M));

	auto it = n.children.begin();
	auto itEnd = n.children.end();

	for (; it != itEnd; ++it) {
		traverseNode(n2m, scene, *it, M);
	}
}

void rasterizeSetBuffers(const tinygltf::Scene & scene) {

	totalNumPrimitives = 0;

	std::map<std::string, BufferByte*> bufferViewDevPointers;

	// 1. copy all `bufferViews` to device memory
	{
		std::map<std::string, tinygltf::BufferView>::const_iterator it(
			scene.bufferViews.begin());
		std::map<std::string, tinygltf::BufferView>::const_iterator itEnd(
			scene.bufferViews.end());

		for (; it != itEnd; it++) {
			const std::string key = it->first;
			const tinygltf::BufferView &bufferView = it->second;
			if (bufferView.target == 0) {
				continue; // Unsupported bufferView.
			}

			const tinygltf::Buffer &buffer = scene.buffers.at(bufferView.buffer);

			BufferByte* dev_bufferView;
			cudaMalloc(&dev_bufferView, bufferView.byteLength);
			cudaMemcpy(dev_bufferView, &buffer.data.front() + bufferView.byteOffset, bufferView.byteLength, cudaMemcpyHostToDevice);

			checkCUDAError("Set BufferView Device Mem");

			bufferViewDevPointers.insert(std::make_pair(key, dev_bufferView));

		}
	}



	// 2. for each mesh: 
	//		for each primitive: 
	//			build device buffer of indices, materail, and each attributes
	//			and store these pointers in a map
	{

		std::map<std::string, glm::mat4> nodeString2Matrix;
		auto rootNodeNamesList = scene.scenes.at(scene.defaultScene);

		{
			auto it = rootNodeNamesList.begin();
			auto itEnd = rootNodeNamesList.end();
			for (; it != itEnd; ++it) {
				traverseNode(nodeString2Matrix, scene, *it, glm::mat4(1.0f));
			}
		}


		// parse through node to access mesh

		auto itNode = nodeString2Matrix.begin();
		auto itEndNode = nodeString2Matrix.end();
		for (; itNode != itEndNode; ++itNode) {

			const tinygltf::Node & N = scene.nodes.at(itNode->first);
			const glm::mat4 & matrix = itNode->second;
			const glm::mat3 & matrixNormal = glm::transpose(glm::inverse(glm::mat3(matrix)));

			auto itMeshName = N.meshes.begin();
			auto itEndMeshName = N.meshes.end();

			for (; itMeshName != itEndMeshName; ++itMeshName) {

				const tinygltf::Mesh & mesh = scene.meshes.at(*itMeshName);

				auto res = mesh2PrimitivesMap.insert(std::pair<std::string, std::vector<PrimitiveDevBufPointers>>(mesh.name, std::vector<PrimitiveDevBufPointers>()));
				std::vector<PrimitiveDevBufPointers> & primitiveVector = (res.first)->second;

				// for each primitive
				for (size_t i = 0; i < mesh.primitives.size(); i++) {
					const tinygltf::Primitive &primitive = mesh.primitives[i];

					if (primitive.indices.empty())
						return;

					// TODO: add new attributes for your PrimitiveDevBufPointers when you add new attributes
					VertexIndex* dev_indices = NULL;
					VertexAttributePosition* dev_position = NULL;
					VertexAttributeNormal* dev_normal = NULL;
					VertexAttributeTexcoord* dev_texcoord0 = NULL;

					// ----------Indices-------------

					const tinygltf::Accessor &indexAccessor = scene.accessors.at(primitive.indices);
					const tinygltf::BufferView &bufferView = scene.bufferViews.at(indexAccessor.bufferView);
					BufferByte* dev_bufferView = bufferViewDevPointers.at(indexAccessor.bufferView);

					// assume type is SCALAR for indices
					int n = 1;
					int numIndices = indexAccessor.count;
					int componentTypeByteSize = sizeof(VertexIndex);
					int byteLength = numIndices * n * componentTypeByteSize;

					dim3 numThreadsPerBlock(128);
					dim3 numBlocks((numIndices + numThreadsPerBlock.x - 1) / numThreadsPerBlock.x);
					cudaMalloc(&dev_indices, byteLength);
					_deviceBufferCopy << <numBlocks, numThreadsPerBlock >> > (
						numIndices,
						(BufferByte*)dev_indices,
						dev_bufferView,
						n,
						indexAccessor.byteStride,
						indexAccessor.byteOffset,
						componentTypeByteSize);


					checkCUDAError("Set Index Buffer");


					// ---------Primitive Info-------

					// Warning: LINE_STRIP is not supported in tinygltfloader
					int numPrimitives;
					PrimitiveType primitiveType;
					switch (primitive.mode) {
					case TINYGLTF_MODE_TRIANGLES:
						primitiveType = PrimitiveType::Triangle;
						numPrimitives = numIndices / 3;
						break;
					case TINYGLTF_MODE_TRIANGLE_STRIP:
						primitiveType = PrimitiveType::Triangle;
						numPrimitives = numIndices - 2;
						break;
					case TINYGLTF_MODE_TRIANGLE_FAN:
						primitiveType = PrimitiveType::Triangle;
						numPrimitives = numIndices - 2;
						break;
					case TINYGLTF_MODE_LINE:
						primitiveType = PrimitiveType::Line;
						numPrimitives = numIndices / 2;
						break;
					case TINYGLTF_MODE_LINE_LOOP:
						primitiveType = PrimitiveType::Line;
						numPrimitives = numIndices + 1;
						break;
					case TINYGLTF_MODE_POINTS:
						primitiveType = PrimitiveType::Point;
						numPrimitives = numIndices;
						break;
					default:
						// output error
						break;
					};


					// ----------Attributes-------------

					auto it(primitive.attributes.begin());
					auto itEnd(primitive.attributes.end());

					int numVertices = 0;
					// for each attribute
					for (; it != itEnd; it++) {
						const tinygltf::Accessor &accessor = scene.accessors.at(it->second);
						const tinygltf::BufferView &bufferView = scene.bufferViews.at(accessor.bufferView);

						int n = 1;
						if (accessor.type == TINYGLTF_TYPE_SCALAR) {
							n = 1;
						}
						else if (accessor.type == TINYGLTF_TYPE_VEC2) {
							n = 2;
						}
						else if (accessor.type == TINYGLTF_TYPE_VEC3) {
							n = 3;
						}
						else if (accessor.type == TINYGLTF_TYPE_VEC4) {
							n = 4;
						}

						BufferByte * dev_bufferView = bufferViewDevPointers.at(accessor.bufferView);
						BufferByte ** dev_attribute = NULL;

						numVertices = accessor.count;
						int componentTypeByteSize;

						// Note: since the type of our attribute array (dev_position) is static (float32)
						// We assume the glTF model attribute type are 5126(FLOAT) here

						if (it->first.compare("POSITION") == 0) {
							componentTypeByteSize = sizeof(VertexAttributePosition) / n;
							dev_attribute = (BufferByte**)&dev_position;
						}
						else if (it->first.compare("NORMAL") == 0) {
							componentTypeByteSize = sizeof(VertexAttributeNormal) / n;
							dev_attribute = (BufferByte**)&dev_normal;
						}
						else if (it->first.compare("TEXCOORD_0") == 0) {
							componentTypeByteSize = sizeof(VertexAttributeTexcoord) / n;
							dev_attribute = (BufferByte**)&dev_texcoord0;
						}

						std::cout << accessor.bufferView << "  -  " << it->second << "  -  " << it->first << '\n';

						dim3 numThreadsPerBlock(128);
						dim3 numBlocks((n * numVertices + numThreadsPerBlock.x - 1) / numThreadsPerBlock.x);
						int byteLength = numVertices * n * componentTypeByteSize;
						cudaMalloc(dev_attribute, byteLength);

						_deviceBufferCopy << <numBlocks, numThreadsPerBlock >> > (
							n * numVertices,
							*dev_attribute,
							dev_bufferView,
							n,
							accessor.byteStride,
							accessor.byteOffset,
							componentTypeByteSize);

						std::string msg = "Set Attribute Buffer: " + it->first;
						checkCUDAError(msg.c_str());
					}

					// malloc for VertexOut
					VertexOut* dev_vertexOut;
					cudaMalloc(&dev_vertexOut, numVertices * sizeof(VertexOut));
					checkCUDAError("Malloc VertexOut Buffer");

					// ----------Materials-------------

					// You can only worry about this part once you started to 
					// implement textures for your rasterizer
					TextureData* dev_diffuseTex = NULL;
					int diffuseTexWidth = 0;
					int diffuseTexHeight = 0;
					if (!primitive.material.empty()) {
						const tinygltf::Material &mat = scene.materials.at(primitive.material);
						printf("material.name = %s\n", mat.name.c_str());

						if (mat.values.find("diffuse") != mat.values.end()) {
							std::string diffuseTexName = mat.values.at("diffuse").string_value;
							if (scene.textures.find(diffuseTexName) != scene.textures.end()) {
								const tinygltf::Texture &tex = scene.textures.at(diffuseTexName);
								if (scene.images.find(tex.source) != scene.images.end()) {
									const tinygltf::Image &image = scene.images.at(tex.source);

									size_t s = image.image.size() * sizeof(TextureData);
									cudaMalloc(&dev_diffuseTex, s);
									cudaMemcpy(dev_diffuseTex, &image.image.at(0), s, cudaMemcpyHostToDevice);
									
									diffuseTexWidth = image.width;
									diffuseTexHeight = image.height;

									checkCUDAError("Set Texture Image data");
								}
							}
						}

						// TODO: write your code for other materails
						// You may have to take a look at tinygltfloader
						// You can also use the above code loading diffuse material as a start point 
					}


					// ---------Node hierarchy transform--------
					cudaDeviceSynchronize();
					
					dim3 numBlocksNodeTransform((numVertices + numThreadsPerBlock.x - 1) / numThreadsPerBlock.x);
					_nodeMatrixTransform << <numBlocksNodeTransform, numThreadsPerBlock >> > (
						numVertices,
						dev_position,
						dev_normal,
						matrix,
						matrixNormal);

					checkCUDAError("Node hierarchy transformation");

					// at the end of the for loop of primitive
					// push dev pointers to map
					primitiveVector.push_back(PrimitiveDevBufPointers{
						primitive.mode,
						primitiveType,
						numPrimitives,
						numIndices,
						numVertices,

						dev_indices,
						dev_position,
						dev_normal,
						dev_texcoord0,

						dev_diffuseTex,
						diffuseTexWidth,
						diffuseTexHeight,

						dev_vertexOut	//VertexOut
					});

					totalNumPrimitives += numPrimitives;

				} // for each primitive

			} // for each mesh

		} // for each node

	}
	

	// 3. Malloc for dev_primitives
	{
		cudaMalloc(&dev_primitives, totalNumPrimitives * sizeof(Primitive));
	}
	

	// Finally, cudaFree raw dev_bufferViews
	{

		std::map<std::string, BufferByte*>::const_iterator it(bufferViewDevPointers.begin());
		std::map<std::string, BufferByte*>::const_iterator itEnd(bufferViewDevPointers.end());
			
			//bufferViewDevPointers

		for (; it != itEnd; it++) {
			cudaFree(it->second);
		}

		checkCUDAError("Free BufferView Device Mem");
	}


}



__global__ 
void _vertexTransformAndAssembly(
	int numVertices, 
	PrimitiveDevBufPointers primitive, 
	glm::mat4 MVP, glm::mat4 MV, glm::mat3 MV_normal, 
	int width, int height) {

	// vertex id
	int vid = (blockIdx.x * blockDim.x) + threadIdx.x;
	if (vid < numVertices) {

		// TODO: Apply vertex transformation here
		// Multiply the MVP matrix for each vertex position, this will transform everything into clipping space
		// Then divide the pos by its w element to transform into NDC space
		// Finally transform x and y to viewport space

		// TODO: Apply vertex assembly here
		// Assemble all attribute arraies into the primitive array
		VertexOut& thisDevVertexOut = primitive.dev_verticesOut[vid];
		//multiply model-view-projective matrix
		glm::vec4 worldSpacePos = MVP * glm::vec4(primitive.dev_position[vid], 1.0f);
		//Projective divide
		glm::vec4 NDCpos = worldSpacePos * (1.0f / worldSpacePos.w);
		//transform into pixels
		glm::vec4 PixelPos = glm::vec4(
			(NDCpos.x + 1.0f) * (float)width / 2.0f,
			(1.0f - NDCpos.y) * (float)height / 2.0f,
			NDCpos.z,
			NDCpos.w);
		//write into vertexout struct
		thisDevVertexOut.pos = PixelPos;
		glm::vec3 eyeSpacePos = glm::vec3(MV * glm::vec4(primitive.dev_position[vid], 1.0f));
		thisDevVertexOut.eyePos = eyeSpacePos;
		glm::vec3 eyeSpaceNormal = glm::normalize(MV_normal * primitive.dev_normal[vid]);
		thisDevVertexOut.eyeNor = eyeSpaceNormal;
		//texture coords
		thisDevVertexOut.texcoord0 = primitive.dev_texcoord0[vid];
		//diffuse texture
		thisDevVertexOut.dev_diffuseTex = primitive.dev_diffuseTex;
		thisDevVertexOut.diffuseTexHeight = primitive.diffuseTexHeight;
		thisDevVertexOut.diffuseTexWidth = primitive.diffuseTexWidth;
	}
}



static int curPrimitiveBeginId = 0;

__global__ 
void _primitiveAssembly(int numIndices, int curPrimitiveBeginId, Primitive* dev_primitives, PrimitiveDevBufPointers primitive) {

	// index id
	int iid = (blockIdx.x * blockDim.x) + threadIdx.x;

	if (iid < numIndices) {

		// TODO: uncomment the following code for a start
		// This is primitive assembly for triangles

		int pid;	// id for cur primitives vector
		if (primitive.primitiveMode == TINYGLTF_MODE_TRIANGLES) {
			pid = iid / (int)primitive.primitiveType;
			dev_primitives[pid + curPrimitiveBeginId].v[iid % (int)primitive.primitiveType]
				= primitive.dev_verticesOut[primitive.dev_indices[iid]];
		}


		// TODO: other primitive types (point, line)
	}
	
}

/**
 * Helper function, determine whether a point is inside a triangle
 */
__device__
bool isPosInTriangle(glm::vec3 p, glm::vec3 pos1, glm::vec3 pos2, glm::vec3 pos3)
{
	glm::vec3 v(p[0], p[1], 0.f);
	glm::vec3 v1(pos1[0], pos1[1], 0.f);
	glm::vec3 v2(pos2[0], pos2[1], 0.f);
	glm::vec3 v3(pos3[0], pos3[1], 0.f);

	//compute areas for barycentric coordinates
	float area = 0.5f * glm::length(glm::cross(v1 - v2, v3 - v2));
	float area1 = 0.5f * glm::length(glm::cross(v - v2, v3 - v2));
	float area2 = 0.5f * glm::length(glm::cross(v - v3, v1 - v3));
	float area3 = 0.5f * glm::length(glm::cross(v - v1, v2 - v1));

	//check the sum of three (signed) areas against the whole area
	return glm::abs(area1 + area2 + area3 - area) < 0.1f;
}

/**
 * Helper function, interpolate depth value
 * Argument list: p is in screen space: (pixel.x, pixel.y, 0)
 * pos1, pos2, pos3 are in combination of screen space and camera space: (pixel.x, pixel.y, eyeSpace.z)
 */

__device__
float depthInterpolate(glm::vec3 p,
	glm::vec3 pos1, glm::vec3 pos2, glm::vec3 pos3)
{
	glm::vec3 v1(pos1[0], pos1[1], 0.f);
	glm::vec3 v2(pos2[0], pos2[1], 0.f);
	glm::vec3 v3(pos3[0], pos3[1], 0.f);

	//compute areas for barycentric coordinates
	float area = 0.5f * glm::length(glm::cross(v1 - v2, v3 - v2));
	float area1 = 0.5f * glm::length(glm::cross(p - v2, v3 - v2));
	float area2 = 0.5f * glm::length(glm::cross(p - v3, v1 - v3));
	float area3 = 0.5f * glm::length(glm::cross(p - v1, v2 - v1));

	//calculate interpolated z:
	float z_inverse_interpolated = area1 / (pos1[2] * area) + area2 / (pos2[2] * area) + area3 / (pos3[2] * area);
	return 1.0f / z_inverse_interpolated;
}

/**
 * Helper function, interpolate any vec2 vertex attributes, mainly for texture coords
 * Argument list: p, pos1, pos2, pos3 are in combination of screen space and camera space: (pixel.x, pixel.y, eyeSpace.z)
 * Attributes to interpolate are attri1, attri2 and attri3
 */
__device__
glm::vec2 vec2AttriInterpolate(glm::vec3 p,
	glm::vec3 pos1, glm::vec3 pos2, glm::vec3 pos3,
	glm::vec2 attri1, glm::vec2 attri2, glm::vec2 attri3)
{
	glm::vec3 v(p[0], p[1], 0.f);
	glm::vec3 v1(pos1[0], pos1[1], 0.f);
	glm::vec3 v2(pos2[0], pos2[1], 0.f);
	glm::vec3 v3(pos3[0], pos3[1], 0.f);

	//compute areas for barycentric coordinates
	float area = 0.5f * glm::length(glm::cross(v1 - v2, v3 - v2));
	float area1 = 0.5f * glm::length(glm::cross(v - v2, v3 - v2));
	float area2 = 0.5f * glm::length(glm::cross(v - v3, v1 - v3));
	float area3 = 0.5f * glm::length(glm::cross(v - v1, v2 - v1));

	//formula:
	// A/Z = Sigma (Ai/Zi * Si/S)
	glm::vec2 AttriIntepolate =
		(attri1 / pos1[2]) * (area1 / area)
		+ (attri2 / pos2[2]) * (area2 / area)
		+ (attri3 / pos3[2]) * (area3 / area);
	return AttriIntepolate * p[2];
}

/**
 * Helper function, interpolate any vec3 vertex attributes
 * Argument list: p, pos1, pos2, pos3 are in combination of screen space and camera space: (pixel.x, pixel.y, eyeSpace.z)
 * Attributes to interpolate are attri1, attri2 and attri3
 * basicall same as vec2 attribute interpolation, but with vec3
 */
__device__
glm::vec3 vec3AttriInterpolate(glm::vec3 p,
	glm::vec3 pos1, glm::vec3 pos2, glm::vec3 pos3,
	glm::vec3 attri1, glm::vec3 attri2, glm::vec3 attri3)
{
	glm::vec3 v(p[0], p[1], 0.f);
	glm::vec3 v1(pos1[0], pos1[1], 0.f);
	glm::vec3 v2(pos2[0], pos2[1], 0.f);
	glm::vec3 v3(pos3[0], pos3[1], 0.f);

	//compute areas for barycentric coordinates
	float area = 0.5f * glm::length(glm::cross(v1 - v2, v3 - v2));
	float area1 = 0.5f * glm::length(glm::cross(v - v2, v3 - v2));
	float area2 = 0.5f * glm::length(glm::cross(v - v3, v1 - v3));
	float area3 = 0.5f * glm::length(glm::cross(v - v1, v2 - v1));

	//formula:
	// A/Z = Sigma (Ai/Zi * Si/S)
	glm::vec3 AttriIntepolate =
		(attri1 / pos1[2]) * (area1 / area)
		+ (attri2 / pos2[2]) * (area2 / area)
		+ (attri3 / pos3[2]) * (area3 / area);
	return AttriIntepolate * p[2];
}


/**
 * For each primitive(persumably triangles here)
 */
__global__ 
void rasterizerFillPrimitive(int numPrimitives, Primitive* primitives,
	Fragment* fragmentBuffer, int* depth, int w, int h)
{
	int primitiveIdx = (blockIdx.x * blockDim.x) + threadIdx.x;
	if (primitiveIdx < numPrimitives)
	{
		Primitive& thisPrimitive = primitives[primitiveIdx];

		//todo: diffuse texture here
		int diffuseTexWidth = thisPrimitive.v[0].diffuseTexWidth;
		int diffuseTexHeight = thisPrimitive.v[0].diffuseTexHeight;
		TextureData* textureData = thisPrimitive.v[0].dev_diffuseTex;
		//get triangle info
		glm::vec3 p1(thisPrimitive.v[0].pos[0], thisPrimitive.v[0].pos[1], thisPrimitive.v[0].eyePos[2]);
		glm::vec3 p2(thisPrimitive.v[1].pos[0], thisPrimitive.v[1].pos[1], thisPrimitive.v[1].eyePos[2]);
		glm::vec3 p3(thisPrimitive.v[2].pos[0], thisPrimitive.v[2].pos[1], thisPrimitive.v[2].eyePos[2]);

		//2D Bounding Box
		float xMin = fminf(p1.x, fminf(p2.x, p3.x));
		float xMax = fmaxf(p1.x, fmaxf(p2.x, p3.x));
		float yMin = fminf(p1.y, fminf(p2.y, p3.y));
		float yMax = fmaxf(p1.y, fmaxf(p2.y, p3.y));

		//get start and end indices in pixel from bounding box
		float xStart = (int)glm::floor(xMin);
		float xEnd = (int)glm::ceil(xMax);

		float yStart = (int)glm::floor(yMin);
		float yEnd = (int)glm::ceil(yMax);

		//scan pixel lines to fill the current primitive(triangle)
		for (int i = yStart; i < yEnd; i++)
		{
			for (int j = xStart; j < xEnd; j++)
			{
				// test if the pos is inside the current triangle
				if (isPosInTriangle(glm::vec3(j, i, 0.f), p1, p2, p3))
				{
					//interpolate depth 
					//depth value is in eye pace
					float z_interpolated = depthInterpolate(glm::vec3(j, i, 0.f), p1, p2, p3);

					//CAUTIOUS(basic)
					//We need to use atomic function to write depth value
					//But it only supports integers, so we multiply it by a large number to get as many digits as we can
					//Note: int32 is bwtween -2^31 and 2^31, which is 2147483648
					int z_rounded = (int)(z_interpolated * 10000.0f);

					//atomic depth writing
					int fragmentIdx = j + (i * w);
					int oldDepth = depth[fragmentIdx];
					int assumed;

					do {
						assumed = oldDepth;
						oldDepth = atomicMin(&depth[fragmentIdx], z_rounded);
					} while (assumed != oldDepth);

					//read depth to perform z-test
					if (z_rounded <= depth[fragmentIdx])
					{
						//the current fragment is in front, so we use it to color
						Fragment& thisFragment = fragmentBuffer[fragmentIdx];

						//The pos of current interested fragment
						glm::vec3 p((float)j, (float)i, z_interpolated);

						//interpolate UV coordinates here 
						glm::vec2 uv_interpolated = vec2AttriInterpolate(p, p1, p2, p3,
							thisPrimitive.v[0].texcoord0, thisPrimitive.v[1].texcoord0, thisPrimitive.v[2].texcoord0);
						//interpolate eyeSpace position here
						glm::vec3 eyeSpacePos_interpolated = vec3AttriInterpolate(p, p1, p2, p3,
							thisPrimitive.v[0].eyePos, thisPrimitive.v[1].eyePos, thisPrimitive.v[2].eyePos);
						thisFragment.eyePos = eyeSpacePos_interpolated;
						//interpolate eye normal here
						glm::vec3 eyeNor_interpolated = vec3AttriInterpolate(p, p1, p2, p3,
							thisPrimitive.v[0].eyeNor, thisPrimitive.v[1].eyeNor, thisPrimitive.v[2].eyeNor);
						//read texture and color the fragment
						glm::ivec2 texSpaceCoord = glm::ivec2(diffuseTexWidth * uv_interpolated.x,
							diffuseTexHeight * uv_interpolated.y);
						int texIdx = texSpaceCoord.x + diffuseTexWidth * texSpaceCoord.y;

						//read from color channels
						int textChannelsNum = 3;
						TextureData r = textureData[texIdx * textChannelsNum];
						TextureData g = textureData[texIdx * textChannelsNum + 1];
						TextureData b = textureData[texIdx * textChannelsNum + 2];

						thisFragment.color = glm::vec3((float)r / 255.0f,
							(float)g / 255.0f,
							(float)b / 255.0f);
					}

				}
			}

		}

	}
}

/**
 * Perform rasterization.
 */
void rasterize(uchar4 *pbo, const glm::mat4 & MVP, const glm::mat4 & MV, const glm::mat3 MV_normal) {
    int sideLength2d = 8;
    dim3 blockSize2d(sideLength2d, sideLength2d);
    dim3 blockCount2d((width  - 1) / blockSize2d.x + 1,
		(height - 1) / blockSize2d.y + 1);

	// Execute your rasterization pipeline here
	// (See README for rasterization pipeline outline.)

	// Vertex Process & primitive assembly
	{
		curPrimitiveBeginId = 0;
		dim3 numThreadsPerBlock(128);

		auto it = mesh2PrimitivesMap.begin();
		auto itEnd = mesh2PrimitivesMap.end();

		for (; it != itEnd; ++it) {
			auto p = (it->second).begin();	// each primitive
			auto pEnd = (it->second).end();
			for (; p != pEnd; ++p) {
				dim3 numBlocksForVertices((p->numVertices + numThreadsPerBlock.x - 1) / numThreadsPerBlock.x);
				dim3 numBlocksForIndices((p->numIndices + numThreadsPerBlock.x - 1) / numThreadsPerBlock.x);

				_vertexTransformAndAssembly << < numBlocksForVertices, numThreadsPerBlock >> >(p->numVertices, *p, MVP, MV, MV_normal, width, height);
				checkCUDAError("Vertex Processing");
				cudaDeviceSynchronize();
				_primitiveAssembly << < numBlocksForIndices, numThreadsPerBlock >> >
					(p->numIndices, 
					curPrimitiveBeginId, 
					dev_primitives, 
					*p);
				checkCUDAError("Primitive Assembly");

				curPrimitiveBeginId += p->numPrimitives;
			}
		}

		checkCUDAError("Vertex Processing and Primitive Assembly");
	}
	
	cudaMemset(dev_fragmentBuffer, 0, width * height * sizeof(Fragment));
	initDepth << <blockCount2d, blockSize2d >> >(width, height, dev_depth);
	
	// TODO: rasterize

	dim3 numThreadsPerBlock(128);

	auto it = mesh2PrimitivesMap.begin();
	auto itEnd = mesh2PrimitivesMap.end();

	for (; it != itEnd; ++it)
	{
		auto p = (it->second).begin();
		auto pEnd = (it->second).end();
		for (; p != pEnd; ++p)
		{
			//launch kernel to fill fragments
			dim3 numBlocksForPrimitives((p->numPrimitives + numThreadsPerBlock.x - 1) / numThreadsPerBlock.x);
			rasterizerFillPrimitive<<<numBlocksForPrimitives, numThreadsPerBlock>>>(p->numPrimitives, dev_primitives, dev_fragmentBuffer,
				dev_depth, width, height);
			checkCUDAError("rasterizerFillPrimitive error!");
			cudaDeviceSynchronize();
		}
	}

    // Copy depthbuffer colors into framebuffer
	render << <blockCount2d, blockSize2d >> >(width, height, dev_fragmentBuffer, dev_framebuffer);
	checkCUDAError("fragment shader");
    // Copy framebuffer into OpenGL buffer for OpenGL previewing
    sendImageToPBO<<<blockCount2d, blockSize2d>>>(pbo, width, height, dev_framebuffer);
    checkCUDAError("copy render result to pbo");
}

/**
 * Called once at the end of the program to free CUDA memory.
 */
void rasterizeFree() {

    // deconstruct primitives attribute/indices device buffer

	auto it(mesh2PrimitivesMap.begin());
	auto itEnd(mesh2PrimitivesMap.end());
	for (; it != itEnd; ++it) {
		for (auto p = it->second.begin(); p != it->second.end(); ++p) {
			cudaFree(p->dev_indices);
			cudaFree(p->dev_position);
			cudaFree(p->dev_normal);
			cudaFree(p->dev_texcoord0);
			cudaFree(p->dev_diffuseTex);

			cudaFree(p->dev_verticesOut);

			
			//TODO: release other attributes and materials
		}
	}

	////////////

    cudaFree(dev_primitives);
    dev_primitives = NULL;

	cudaFree(dev_fragmentBuffer);
	dev_fragmentBuffer = NULL;

    cudaFree(dev_framebuffer);
    dev_framebuffer = NULL;

	cudaFree(dev_depth);
	dev_depth = NULL;

    checkCUDAError("rasterize Free");
}
